WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* ~ "\n" }

rule_set = { SOI ~ (COMMENT | rule)* ~ EOI }

rule = {
    rule_header ~ rule_outcome ~
    "if" ~ condition ~ ("and" ~ condition)* ~ "."
}

rule_header = { label? ~ "A " ~ object_selector }
label = @{ (!(". A ") ~ ANY)+ ~ ". " }

object_selector = @{ "**" ~ identifier ~ "**" }
selector = @{ identifier }

rule_outcome = {
  outcome_verb ~ outcome
}
outcome_verb = {
  "gets" | "passes" | "is" | "has" | "receives" | "qualifies for" | "meets" | "satisfies"
}

outcome = @{ (!("if" | ".") ~ ANY)+ }

condition = {
    property_condition |
    rule_reference |
    label_reference
}

property_condition = {
    "the" ~ property ~ "of" ~ "the" ~ object_selector ~ predicate
}

rule_reference = {
    "the" ~ object_selector ~ reference_verb ~ reference_object?
}

label_reference = { "ยง" ~ label_name ~ label_predicate? }
label_name = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }
label_predicate = _{ "is valid" | "is approved" | "has passed" }

reference_verb = {
    "passes" | "meets" | "satisfies" | "qualifies for" | "is eligible for" | "has passed"
}

reference_object = @{ (!(" and " | ".") ~ ANY)+ }

predicate = {
    comparison_operator ~ value |
    list_operator ~ list_value
}

list_operator = {
    "is in" |
    "is not in"
}

comparison_operator = {
    "is greater than or equal to" |
    "is less than or equal to" |
    "is equal to" |
    "is not equal to" |
    "is the same as" |
    "is not the same as" |
    "is later than" |
    "is earlier than" |
    "is greater than" |
    "is less than" |
    "contains"
}

list_value = { "[" ~ value ~ ("," ~ value)* ~ "]" }
value = { number | string_literal | date_literal | boolean }

rule_name = @{(!(". " | " and " | ".") ~ ANY)+}
property = @{ "__" ~ property_name ~ "__" }
property_name = @{ (!("__") ~ ANY)+ }
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
string_literal = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\"" | identifier }
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
date_format = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }
date_literal = @{
  "date(" ~ date_format ~ ")" |
  date_format
}
boolean = { "true" | "false" }
